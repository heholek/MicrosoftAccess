Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Compare Database
Option Explicit

Private cmdLoginByName As New ADODB.Command
Private cmdLoginById As New ADODB.Command
Private cmdLoginByNameOnly As New ADODB.Command

Public Id As Variant
Public Code As Variant
Public Name As Variant
Public Active As Boolean

Public Sub LoginByNameOnly(LoginName, Optional windowsLogon As Boolean = True)
Dim rs As ADODB.Recordset

    cmdLoginByNameOnly.parameters(0) = LoginName
    Set rs = cmdLoginByNameOnly.Execute
    SetFields rs
    If Me.Id = 0 And windowsLogon Then
        Me.Id = -1
        Me.Code = LoginName
        Me.Name = LoginName
        Me.Active = True
    End If
End Sub
Public Sub LoginById(UserId As Long, Password As String)
Dim rs As ADODB.Recordset

    cmdLoginById.parameters(0) = UserId
    cmdLoginById.parameters(1) = Password
    Set rs = cmdLoginById.Execute
    SetFields rs
End Sub

Public Sub LoginByName(LoginName As String, Password As String)
Dim rs As ADODB.Recordset

    cmdLoginByName.parameters(0) = LoginName
    cmdLoginByName.parameters(1) = Password
    Set rs = cmdLoginByName.Execute
    SetFields rs
End Sub
Private Sub Class_Initialize()
    ClearFields
    cmdLoginByName.commandText = "SELECT * FROM Users WHERE Login=? AND Password=?"
    cmdLoginByName.ActiveConnection = CurrentProject.Connection
    cmdLoginByName.parameters.Refresh
    cmdLoginById.commandText = "SELECT * FROM Users WHERE Id=? AND Password=?"
    cmdLoginById.ActiveConnection = CurrentProject.Connection
    cmdLoginById.parameters.Refresh
    cmdLoginByNameOnly.commandText = "SELECT * FROM Users WHERE Login=?"
    cmdLoginByNameOnly.ActiveConnection = CurrentProject.Connection
    cmdLoginByNameOnly.parameters.Refresh
End Sub

Private Sub Class_Terminate()
    Set cmdLoginByName = Nothing
    Set cmdLoginById = Nothing
    Set cmdLoginByNameOnly = Nothing
End Sub

Private Sub SetFields(rs As ADODB.Recordset)
    On Error Resume Next
    If Not rs.EOF Then
        Me.Id = rs("Id")
        Me.Code = rs("Code")
        Me.Name = rs("Name")
        Me.Name = Nz(rs("FirstName"), "") & " " & Nz(rs("LastName"), "")
        If omStringFunctions.IsNullOrEmpty(Me.Name) Then
            Me.Name = rs("Login")
        End If
        Me.Active = Nz(rs("Active"), False)
    Else
        ClearFields
    End If
    rs.Close
    Set rs = Nothing
End Sub
Private Sub ClearFields()
    Me.Id = Null
    Me.Code = Null
    Me.Name = Null
    Me.Active = False
End Sub